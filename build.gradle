plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
}

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = getVersionFromJava()
group = config.mod_group
archivesBaseName = config.mod_name

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "minecraft"
    mappings = "snapshot_20141130"
    replace '${version}', project.version
}

repositories {
    mavenCentral()
    maven {
        name = "ilexiconn"
        url = "http://maven.ilexiconn.net/"
    }
}

configurations {
    include
}

dependencies {
    compile "net.ilexiconn:llibrary:${config.llibrary_version}-${config.minecraft_version}:dev"
    include "net.ilexiconn:llibrary-wrapper:${config.minecraft_version}"
}

jar {
    from configurations.include.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes "FMLCorePlugin": "net.ilexiconn.llibrary.LLibraryWrapper"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

String getVersionFromJava() {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String VERSION = \"";
    File file = file("src/main/java/com/github/alexthe666/iceandfire/IceAndFire.java")
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }

    return "$major.$revision.$patch";
}
